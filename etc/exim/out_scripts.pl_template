#!/usr/bin/env perl

use v5.40;
use warnings;
use utf8;

use lib "__SRCDIR__/lib";
use Email();
use Digest::MD5 qw(md5_hex);
use ReadConfig();

sub want_tag ($local_part, $domain_part) {
  my $user = $local_part."@".$domain_part;

  my $email = Email::create($user);
  my $delivery_type = $email->get_pref('delivery_type', 1);
  if ($delivery_type == 1) {
    return 1;
  }
  return 0;
}

sub get_tagged_subject {
  my $local_part = Exim::expand_string('$local_part');
  my $domain_part = Exim::expand_string('$domain');
  my $subject = Exim::expand_string('$header_subject');
  my $user = $local_part."@".$domain_part;

  my $email = Email::create($user);
  my $tag = $email->get_pref('spam_tag', '{Spam?}');

  $subject =~ s/{ST_SPAM}/$tag/;
  return "Subject: ".$subject;
}

sub get_plain_subject {
  my $subject = Exim::expand_string('$header_subject');

  $subject =~ s/{ST_SPAM}//;
  return "Subject: ".$subject;
}

sub get_no_news_tag_subject {
  my $subject = Exim::expand_string('$rh_subject');

  $subject =~ s/{ST_NEWS}//;
  return "Subject:".$subject;
}

sub is_blacklisted ($local_part, $domain_part) {
  # Get parameters values
  my $sender      = Exim::expand_string('$sender_address');
  my $sender_body = Exim::expand_string('$header_From');
  my $user        = $local_part . "@" . $domain_part;

  my $email = Email::create($user);
  my $blacklisted = ($email->has_in_white_warn_list('blacklist', $sender) || $email->has_in_white_warn_list('blacklist', $sender_body));
  my $header_status = Exim::expand_string('$header_X-SpamTagger-Status');
  my $blacklist_hash = get_blacklisted_flag($local_part, $domain_part);

  if ($header_status =~ m/\Q$blacklist_hash/ && $blacklisted) {
    return 0;
  } else {
    return $blacklisted;
  }
}

## Generate md5 sum flag for blacklisted
sub get_blacklisted_flag ($local_part, $domain_part) {
  my $sender      = Exim::expand_string('$sender_address');
  my $sender_body = Exim::expand_string('$header_From');
  my $user        = $local_part . "@" . $domain_part;

  my $email = Email::create($user);

  # The blacklist works on MAIL FROM (SMTP) and FROM Body
  my $blacklisted = $email->has_in_white_warn_list('blacklist', $sender_body);
  if ($blacklisted)  {
     $sender = $sender_body;
  }

  my $conf = ReadConfig::get_instance();
  my $client_id = $conf->get_option('CLIENTID');
  my $blacklist_digest = $client_id . $sender . $client_id;

  return md5_hex($blacklist_digest);
}

sub quarantine_bounce ($local_part, $domain_part) {
  my $user = $local_part."@".$domain_part;

  my $email = Email::create($user);
  my $quarbounce = $email->get_pref('quarantine_bounces', 0);

  return $quarbounce;
}
