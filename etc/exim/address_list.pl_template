#!/usr/bin/env perl

use v5.40;
use warnings;
use utf8;

## pushed format must be
## address(,user)

use lib "__SRCDIR__/lib";
use Net::SMTP();
use ReadConfig();
use DB();

my $ret = '';
my $domain = shift;
my $sender = shift;
my $recipient = shift;

my $conf = ReadConfig::get_instance();

my $afile = $conf->get_option('VARDIR')."/spool/spamtagger/addresses/".$domain.".addresslist";

my $AFILE;
open($AFILE, ">", $afile) or die "Cannot open destination file: $afile";

my $in_headers = 1;
my $full_text = '';
while (<>) {
  if (/^\s*$/) {
    $in_headers = 0;
  }
  next if $in_headers;

  $full_text .= $_;

  if (/([^@,]+)\@$domain([,;])?(\S+)?/) {
    print $AFILE "$1\n";
  }
}
close $AFILE;

print "OK ".$ret." - address list saved in $afile\n";

if ($conf->get_option('ISMASTER') =~ m/^(Y|y|yes|YES)$/) {
  my $replica_db = DB->db_connect('replica', 'st_config');
  my @sources = $replica_db->getListOfHash("SELECT hostname from source");
  my @replicas = $replica_db->getListOfHash("SELECT hostname from replica");
  $replica_db->db_disconnect();
  foreach my $s (@replicas) {
    my $do = 1;
    foreach my $m (@sources) {
      if ($m->{'hostname'} eq $s->{'hostname'}) {
        $do = 0;
      }
      if ($do) {
        send_to_replica($s->{'hostname'});
      }
    }
  }
}

exit 0;

sub send_to_replica ($replica) {
  my $smtp = Net::SMTP->new($replica);
  if (!$smtp) {
    print STDERR "Cannot connect to $replica for propagating address list\n";
    return 0;
  }
  $smtp->mail($sender);
  $smtp->to($recipient);
  my $err = $smtp->code();
  if ($err > 299) {
    print STDERR "Cannot send propagation message to $replica, error code: $err\n";
    return 0;
  }
  $smtp->data();
  $smtp->datasend($full_text);
  $smtp->dataend();
  $err = $smtp->code();
  if ($err < 300) {
    print "Porpagation done to $replica\n";
    return 1;
  }
  print STDERR "Propagation failed to $replica\n";
  return 0;
}
